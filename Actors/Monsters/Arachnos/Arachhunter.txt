ACTOR Arachhunter : Arachnotron 23400
{
  Health 1500
  PainChance 70
  Scale 1.2
  Speed 13
  Radius 52
  SeeSound "Arachhunter/sight"
  ActiveSound "Arachhunter/active"
  PainSound "Arachhunter/pain"
  DeathSound "Arachhunter/death"
  AttackSound ""
  DropItem "BossLifeEssence" 200 25
  DropItem "BossArmorBonusMax" 200 1
  DropItem "NewCellPack" 140
  DamageFactor "Baby15K", 0.0
  DropItem "Railgun "
  DropItem "Assault Shotgun"
  Species "BabySpiders"
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +AMBUSH
  Obituary "%o was targeted and hunted down by a Arachhunter."
  Tag "$NEArachh"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ModifyMonster",0,1)
	Goto Idle
  Idle:
    AHHN A 10 A_Look
    Loop
  See:
    AHHN A 2 A_BabyMetal
    AHHN ABBCC 2 A_Chase
    AHHN D 2 A_BabyMetal
    AHHN DEEFF 2 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_Jump(82,"BFG10K","Railgun")
  Normal:
	AHHN G 8 A_FaceTarget
	AHHN G 6 A_FaceTarget
	TNT1 A 0 A_SpawnItemEx("ShellCasing", 8, 12, 28, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128)
	TNT1 AAA 0 A_CustomMissile("ProjectileBullet",20,0,Random(-1000,1000)/100.00,0)
	TNT1 A 0 A_CustomBulletAttack(22.5, 18.3, 4, Random(1,5)*3, "ArachhunterPuff")
	AHHN H 2 Bright A_PlaySoundEx("AShotgun/Fire","Weapon")
	TNT1 A 0 A_Jump(30,"Missile")
    TNT1 A 0 A_SpidRefire
	Goto Normal+2
  Railgun: 
	TNT1 A 0 A_JumpifCloser(200,"Normal")
    TNT1 A 0 A_PlaySound("weapons/rgcharge")
    AHHN G 10 A_FaceTarget
	AHHN G 5 Bright A_FaceTarget
	AHHN H 4 Bright A_CustomMissile("ArachhunterRail",20,0,0) 
    AHHN G 15 Bright A_FaceTarget	
	TNT1 A 0 A_Jump(40,"BFG10K")
	TNT1 A 0 A_Jump(50,"See")
	TNT1 A 0 A_SpidRefire
    Goto Railgun+1
  BFG10K:
	TNT1 A 0 A_PlaySound("weapons/charge15k")
    AHHN G 18 Bright A_FaceTarget
  BFG10KFire:
	AHHN G 8 Bright A_FaceTarget
	AHHN G 2 Bright A_FaceTarget
	TNT1 A 0 A_PlaySound("weapons/bfg15kfire")
    AHHN H 4 Bright A_CustomMissile("Arachhunter10K",20,0,Random(4,-4))
	TNT1 A 0 A_Jump(12,"Pause")
	TNT1 A 0 A_SpidRefire
    Goto BFG10KFire+1
  Pause:
	AHHN G 20 A_PlaySound("weapons/bfg15kcooldown")
	Goto See
  Pain:
    TNT1 A 0 A_Jump(87,"PainMissile")
    AHHN I 12 A_Pain
    Goto See
  PainMissile:
	AHHN I 12 A_Pain
    Goto Missile
  Death:
    AHHN I 20 A_Scream
	AHHN J 6
	TNT1 AAA 0 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	AHHN KKKKK 2 Bright A_CustomMissile("CFKaboom2", random(20,40), random(10,-10), random(0,360), 2, random(0,360))
	TNT1 AAAA 0 A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360))
    AHHN L 6 A_NoBlocking
    AHHN MN 6
    AHHN O 6
    AHHN P -1 A_KillMaster
    Stop
  Raise:
  	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    AHHN PONMLKJ 5
    Goto See+1
  }
}

ACTOR Arachhunter10K : Red10KProjectile
{
Obituary "%o was targeted on sight by a Arachhunter."
States
{
Death:
XDeath:  
   TNT1 A 0
   TNT1 A 1 A_SpawnItem("Arachhunter10KEx")
   Stop
   }
}

ACTOR Arachhunter10KEx : Red10KEx
{
DamageType "Baby15K"
}


ACTOR ArachhunterRail : FastProjectile
{
Radius 5
Height 5
Speed 300
Damage (Random(40,60))
Decal BFG9500Lightning
Scale 1.15
SeeSound "monsters/darkcyberrail"
MissileType "CardinalRailTrail"
DamageFactor "Baby15K", 0.0
Species "BabySpiders"
DeathSound "cyberrail/impact"
+RIPPER
+THRUSPECIES
+BLOODLESSIMPACT
States
{
Spawn:
    DCRP A 1 Bright
    Loop
Death:
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("BFGShockwaveRed", 0, 0, 0, 0, 0, 0, 0, 128)
    TNT1 AAAA 0 A_CustomMissile("Kaboom14", 0, 0, random(0,360), 2, random(0,360))	
	TNT1 A 1 A_SpawnItem("RailgunImpactEffectRed")
	Stop
	}
}

ACTOR ArachhunterPuff : BulletPuff
{
DamageType "Baby15K"
}