ACTOR ArachhunterMKII : Arachnotron 23401
{
  Health 1750
  PainChance 50
  Scale 1.2
  Speed 13
  Radius 52
  SeeSound "Arachhunter/sight"
  ActiveSound "Arachhunter/active"
  PainSound "Arachhunter/pain"
  DeathSound "Arachhunter/death"
  AttackSound ""
  DropItem "BossLifeEssence" 200 25
  DropItem "BossArmorBonusMax" 200 1
  DropItem "NewCellPack" 140
  DamageFactor "Baby15K", 0.0
  DropItem "Explosive Shotgun" 200
  DropItem "ExplosiveRailgun" 64
  Species "BabySpiders"
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +AMBUSH
  Obituary "%o was targeted and hunted down by a Arachhunter MK II."
  Tag "NEArachhMKii"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ModifyMonster",0,1)
	Goto Idle
  Idle:
    AHHU A 10 A_Look
    Loop
  See:
    AHHU A 2 A_BabyMetal
    AHHU ABBCC 2 A_Chase
    AHHU D 2 A_BabyMetal
    AHHU DEEFF 2 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_Jump(82,"BFG10K","Railgun")
  Normal:
	AHHU G 14 A_FaceTarget
	TNT1 A 0 A_PlaySound("weapons/ExplosiveSG/fire",7)
	TNT1 A 0 A_SpawnItemEx("OrangeShellCasing", 8, 12, 28, Random(2, 6), 0, Random(1, 3), Random(-80, -90), 128)
	TNT1 AAAAAA 0 Bright A_CustomMissile("ArachhunterExplosiveTracer",20,0,Random(15,-15),0)
	AHHU H 4 Bright A_CustomBulletAttack(18,6,6,Random(5,7),"ArachhunterPuff")
	AHHU G 12
    TNT1 A 0 A_Jump(128,"See")
    TNT1 A 0 A_SpidRefire
	Goto Normal+1
  Railgun:
    TNT1 A 0 A_PlaySound("ExplosiveRailgun/Charge")
    AHHU G 10 A_FaceTarget
	AHHU G 5 Bright A_FaceTarget
	TNT1 A 0 A_PlaySound("ExplosiveRailgun/Fire")
	TNT1 A 0 A_CustomMissile("ArachExplosiveRailgunImpact",20,0,0)
	AHHU H 5 Bright A_CustomRailgun(50,0,"None","None",RGF_SILENT,1,0,"FusioniteRailPuff",0,0,0,0,40,0,"ExplosiveRailgunTrail")
    AHHU G 20 Bright A_FaceTarget	
	TNT1 A 0 A_Jump(40,"BFG10K")
	TNT1 A 0 A_Jump(50,"See")
	TNT1 A 0 A_JumpIfCloser(300,"Normal")
	TNT1 A 0 A_SpidRefire
    Goto Normal+1
  BFG10K:
	TNT1 A 0 A_PlaySound("weapons/charge15k")
    AHHU G 18 Bright A_FaceTarget
  BFG10KFire:
	AHHU G 8 Bright A_FaceTarget
	AHHU G 2 Bright A_FaceTarget
	TNT1 A 0 A_PlaySound("weapons/bfg15kfire")
    AHHU H 4 Bright A_CustomMissile("Arachhunter10K",20,0,Random(4,-4))
	TNT1 A 0 A_Jump(12,"Pause")
	TNT1 A 0 A_SpidRefire
    Goto BFG10KFire+1
  Pause:
	AHHU G 20 A_PlaySound("weapons/bfg15kcooldown")
	Goto See
  Pain:
    TNT1 A 0 A_Jump(87,"PainMissile")
    AHHU I 12 A_Pain
    Goto See
  PainMissile:
	AHHU I 12 A_Pain
    Goto Missile
  Death:
    AHHU I 20 A_Scream
	AHHU J 6
	TNT1 AAA 0 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	AHHU KKKKK 2 Bright A_CustomMissile("CFKaboom2", random(20,40), random(10,-10), random(0,360), 2, random(0,360))
	TNT1 AAAA 0 A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360))
    AHHU L 6 A_NoBlocking
    AHHU MN 6
    AHHU O 6
    AHHU P -1 A_KillMaster
    Stop
  Raise:
  	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    AHHU PONMLKJ 5
    Goto See+1
  }
}

ACTOR ArachExplosiveRailgunImpact : ExplosiveRailgunImpact
{
MissileType "ArachExRailTrailExplodeSpawner"
}

ACTOR ArachExRailTrailExplodeSpawner
{
  States
  {
  Spawn:
	TNT1 A 1 Nodelay A_SpawnItemEx("ArachExRailTrailExplode",random(0,4),0,0,random(0,4),0,random(-4,4),random(0,360))
	Stop
  }
}

ACTOR ArachExRailTrailExplode : ExplosiveRailgunTrailExplode
{
  Damagetype "Baby15K"
  Species "BabySpiders"
  Tag "\c[Q3]Arachhunter MK II\c-"
  Obituary "%o was targeted on sight by a Arachhunter MK II."
  States
  {
  Spawn:
	TNT1 A 0
	FXPO AB 2 Bright A_FadeOut(0.05)
	TNT1 A 0 A_Explode(30,128,0,0,40)
	FXPO CD 1 Bright A_FadeOut(0.05)
	TNT1 A 0 A_Explode(30,128,0,0,40)
	FXPO EF 1 Bright A_FadeOut(0.05)
	TNT1 A 0 A_Explode(30,128,0,0,40)
	FXPO GHIJKLM 1 Bright A_FadeOut(0.05)
	Stop
  }
}

ACTOR ArachhunterExplosiveTracer : CybernobleExplosiveTracer
{
DamageType "Baby15K"
}