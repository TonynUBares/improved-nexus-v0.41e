Actor BLGGiver : CustomInventory
{
  Scale 0.9
  Inventory.Amount 1
  Inventory.MaxAmount 0
  Inventory.InterHubAmount 0
  Inventory.PickupSound "Weapons/BLG/Get"
  Obituary "%o Got Electrocuted by %k's Big Lightning Gun."
  Inventory.PickupMessage "You got the Big Lightning Gun!"
  Tag "Big Lightning Gun"
  States
  {
  Spawn:
	PULX A 0 NoDelay ACS_NamedExecuteWithResult("WeaponListCheck",7,0)
  SpawnLoop:
	PULX A -1
	Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("BLG",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("BLG",1)
	TNT1 A 0 A_JumpIfInventory("BLGGiverCheck",1,"HoldAndDestroy")
	TNT1 A 0 A_GiveInventory("BLGGiverCheck",1)
	TNT1 A 0 A_GiveInventory("DCell",40)
	Stop
  JustAmmo:
    TNT1 A 0 A_JumpIfInventory("BLGGiverCheck",1,"Null")
	TNT1 A 0 A_GiveInventory("BLGGiverCheck",1)
	TNT1 A 0 A_GiveInventory("DCell",40)
	TNT1 A 0 A_SpawnItemEx("BLG")
	Stop
  }
}

Actor BLGSpawnerGiver : BLGGiver
{
  States
  {
  JustAmmo:
    TNT1 A 0 A_JumpIfInventory("BLGGiverCheck",1,"Null")
	TNT1 A 0 A_GiveInventory("BLGGiverCheck",1)
	TNT1 A 0 A_GiveInventory("DCell",40)
	Stop
  }
}

Actor BLGGiverCheck : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }

Actor BLG : Weapon 
{
 Tag "Big Lightning Gun"
 Inventory.PickupSound "Weapons/BLG/Get"
 Weapon.UpSound "Weapons/BLG/up"
 Obituary "%o Got Electrocuted by %k's Big Lightning Gun."
 Inventory.PickupMessage "You got the Big Lightning Gun!"
 Weapon.AmmoUse 0
 Weapon.AmmoGive 80
 Weapon.AmmoType "DCell"
 Weapon.AmmoUse2 0
 Weapon.AmmoType2 "DCell"
 Weapon.SlotNumber 7
 Weapon.SlotPriority 9.2
 Decal DBFGLightning
 +WEAPON.NOAUTOFIRE
 +WEAPON.NOAUTOAIM
 States
 {
  Spawn:
	PULX A -1
	Loop
  Ready:
	TNT1 A 0 A_PlaySound("Weapons/BLG/loop",CHAN_6,1.0,1)
  ReadyLoop:
	REPT AAAAAAAAAAAAAA 5 A_WeaponReady
	Loop
  Deselect:
	TNT1 A 0 ACS_NamedExecuteWithResult("SbarFix",1)
	TNT1 A 0 A_PlaySound("Weapons/BLG/down",CHAN_6)
	REPT A 1 A_Lower
	Wait
  Select:
	TNT1 A 0 ACS_NamedExecuteWithResult("SbarFix",0)
	REPT A 1 A_Raise
	Goto Select+1
  Fire:
	TNT1 A 0 A_JumpIfInventory("DCell",20,1)
	Goto Ready
	REPT A 4
	TNT1 A 0 A_PlaySound("Weapons/BLG/fire",CHAN_WEAPON)
	TNT1 A 0 A_FireCustomMissile("BLGSuperLightningBall",0,1)
	REPT A 0 A_GunFlash
	TNT1 A 0 A_TakeInventory("DCell",20)
	REPT ABCD 2 A_SetPitch (pitch-0.50)
	REPT D 10 A_Recoil(3)
	REPT DDDD 2 A_SetPitch (pitch+0.50)
	REPT C 8
	REPT CCBA 4
	REPT A 10 A_ReFire
	REPT A 10 
	Goto Ready
  FireNoAmmo:
	REPT A 4
	TNT1 A 0 A_PlaySound("Weapons/BLG/fire",CHAN_WEAPON)
	TNT1 A 0 A_FireCustomMissile("BLGSuperLightningBall",0,1)
	REPT A 0 A_GunFlash
	REPT ABCD 2 A_SetPitch (pitch-0.50)
	REPT D 10 A_Recoil(3)
	REPT DDDD 2 A_SetPitch (pitch+0.50)
	REPT C 8
	REPT CCBA 4
	REPT A 10 A_ReFire
	REPT A 10 
	Goto Ready
  Flash:
	PREX ABC 2 Bright A_Light1
	Goto lightdone	
  AltFire:
	TNT1 A 0 A_JumpIfInventory("DCell",7,1)
	Goto Ready
	REPT A 4
	TNT1 A 0 A_PlaySound("Weapons/BLG/fire",CHAN_WEAPON)
	TNT1 A 0 A_FireCustomMissile("BLGSeekerLauncherSpawner",0,1,0,0,0,0)
	REPT A 0 A_GunFlash
	TNT1 A 0 A_TakeInventory("DCell",7)
	REPT ABC 3
	REPT C 17
	REPT C 8// A_PlaySound("Weapons/BLG/Charge",CHAN_6)
	REPT CCBA 4
	REPT A 10 A_ReFire
	REPT A 10 
	Goto Ready
  AltfireNoAmmo:
	REPT A 4
	TNT1 A 0 A_PlaySound("Weapons/BLG/fire",CHAN_WEAPON)
	TNT1 A 0 A_FireCustomMissile("BLGSeekerLauncherSpawner",0,1,0,0,0,0)
	REPT A 0 A_GunFlash
	REPT ABC 3
	REPT C 17
	REPT C 8// A_PlaySound("Weapons/BLG/Charge",CHAN_6)
	REPT CCBA 4
	REPT A 10 A_ReFire
	REPT A 10 
	Goto Ready
  AltFlash:
	PREX ABC 3 Bright A_Light1
	goto lightdone	
 }
}

ACTOR BLGSuperLightningBall
{
	RenderStyle Translucent
	Alpha 0.80
	Scale 1.5
	Radius 24
	Height 48
	Speed 60
	PROJECTILE
	Damage 150
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+DONTHARMSPECIES
	+MTHRUSPECIES
	Species "Player"
	DamageType "DBFG2"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 Bright A_PlaySound("Weapons/BLG/fly",CHAN_5,1.0,1)
	SpawnLoop:
		BLSB A 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB B 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB C 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB D 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB E 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB F 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB G 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB H 4 Bright A_CustomMissile("BLGLightningBolt",0)
		TNT1 A 0 A_SpawnItemEx("BLGRailAttack",0,0,0,0,0,0,0,SXF_SETMASTER)
		TNT1 A 0 A_Explode(64, 128,0)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Weapons/BLG/charge",CHAN_5,1.0,0,ATTN_NONE)
		TNT1 A 0 A_Explode(64, 128,0)
		BLSB ABCDEFGHABCDEFGHABCDEFGHABCD 1 Bright
		BLSB B 0 A_ChangeFlag("NODAMAGETHRUST",False)
		BLSB B 0 A_PlaySound("Weapons/BLG/bang",CHAN_5,1.0,0,ATTN_NONE)
		THT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BLGSpark",0,0,Random(0,359),CMF_TRACKOWNER|CMF_AIMDIRECTION,Random(-90,90)) //40
		BLSB EFGHIIJJKKLLMM 2 Bright A_Explode(192,384)
		Stop
	}
}

ACTOR BLGRailAttack
{
  PROJECTILE
  +LOOKALLAROUND
  +ISMONSTER
  +QUICKTORETALIATE
  +NOTARGET
  +FRIENDLY
  +NOCLIP
  +THRUSPECIES
  +DONTHARMSPECIES
  Species "Player"
  DamageType "Lightning"
  Obituary "%o was zapped by %k's Big Lightning Gun."
  States
  {
  Spawn:
    TNT1 A 0
	TNT1 AAAAA 1 A_Look
	Stop
  See:
    TNT1 A 0
	TNT1 A 1 A_JumpIfCloser(8192,"Attack")
	Stop
  Attack:
    TNT1 A 0
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomRailgun(-1,0,NONE,"LightCyan",RGF_SILENT,1,16,"BLGRailPuff",0,0,8192,12,0.75)
	Goto Death	
  Death:
	TNT1 A 1
	Stop
  }
}

ACTOR BLGRailPuff : BulletPuff
{
  -ALLOWPARTICLES
  +ALWAYSPUFF
  +PUFFONACTORS
  +FORCERADIUSDMG
  +NODAMAGETHRUST
  +PUFFGETSOWNER
  +MTHRUSPECIES
  -CLIENTSIDEONLY
  +EXTREMEDEATH
  Scale 0.175
  VSpeed 0
  DamageType "Lightning"
  Obituary "%o was zapped by %k's Big Lightning Gun."
  States
  {
  Spawn:
  Missile:
    TNT1 A 1
	TNT1 A 1 A_Explode (128, 16, FALSE, FALSE, 16)
	stop
  }
}

Actor BLGLightningbolt 
{
	Radius 1
	Height 2
	Speed 20
	Damage 0
	Scale 0.5
	Projectile
	+NOBLOCKMAP
	+ACTIVATEPCROSS
	+NOTELEPORT
	Renderstyle Add
	Alpha 0.65
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"1A","1B","1C","1D","1E","1F","1G","1H","1I",
						"2A","2B","2C","2D","2E","2F","2G","2H","2I",
						"3A","3B","3C","3D","3E","3F","3G","3H","3I",
						"4A","4B","4C","4D","4E","4F","4G","4H","4I")
		Stop
	1A:
		HBL1 A 4
		Stop
	1B:
		HBL1 B 4
		Stop
	1C:
		HBL1 C 4
		Stop
	1D:
		HBL1 D 4
		Stop
	1E:
		HBL1 E 4
		Stop
	1F:
		HBL1 F 4
		Stop
	1G:
		HBL1 G 4
		Stop
	1H:
		HBL1 H 4
		Stop
	1I:
		HBL1 I 4
		Stop
	2A:
		HBL2 A 4
		Stop
	2B:
		HBL2 B 4
		Stop
	2C:
		HBL2 C 4
		Stop
	2D:
		HBL2 D 4
		Stop
	2E:
		HBL2 E 4
		Stop
	2F:
		HBL2 F 4
		Stop
	2G:
		HBL2 G 4
		Stop
	2H:
		HBL2 H 4
		Stop
	2I:
		HBL2 I 4
		Stop
	3A:
		HBL3 A 4
		Stop
	3B:
		HBL3 B 4
		Stop
	3C:
		HBL3 C 4
		Stop
	3D:
		HBL3 D 4
		Stop
	3E:
		HBL3 E 4
		Stop
	3F:
		HBL3 F 4
		Stop
	3G:
		HBL3 G 4
		Stop
	3H:
		HBL3 H 4
		Stop
	3I:
		HBL3 I 4
		Stop
	4A:
		HBL4 A 4
		Stop
	4B:
		HBL4 B 4
		Stop
	4C:
		HBL4 C 4
		Stop
	4D:
		HBL4 D 4
		Stop
	4E:
		HBL4 E 4
		Stop
	4F:
		HBL4 F 4
		Stop
	4G:
		HBL4 G 4
		Stop
	4H:
		HBL4 H 4
		Stop
	4I:
		HBL4 I 4
		Stop
	}
}

ACTOR BLGSpark
{
	Speed 25
	Scale 0.5
	Radius 4
	Height 8
	RenderStyle Add
	Alpha 0.75
	Damage 1
	DamageType "lightning"
	Projectile
	-NOGRAVITY
	+RIPPER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+MTHRUSPECIES
	States
	{
	Spawn:
		G3SP AB 2 Bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Weapons/BLG/Spark",CHAN_7)
		G3SP CDEF 2 Bright A_Explode(64,64)
		Stop
	}
}


ACTOR BLGSeekerLauncherSpawner : PlasmaBall
{
	Scale 0.5
	Radius 16
	Height 16
	Damage 64
	DamageType "Lightning"
	RenderStyle "Add"
	Alpha 0.85
	SeeSound "NoSound"
	DeathSound "NoSound"
	Obituary "%o got in the way of %k's BLG Seeker."
	+MTHRUSPECIES
	States
	{
	Spawn:
		B3S1 ABCD 4 Bright
		goto Death
	Death:
		TNT1 A 0 A_SpawnitemEx("BLGSeekerLauncher",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION,0)
		Stop
	}
}

ACTOR BLGSeekerLauncher
{
	Scale 0.5
	RenderStyle "Add"
	Alpha 0.90
	Radius 16
	Height 32
	Mass 1000
	Health 1000
	Speed 32
	Damage 128
	DamageType "Lightning"
	DamageFactor "Lightning", 0
	DamageFactor "LegendaryPlayer", 0
	PainChance 32
	PainThreshold 50
	Species "Player"
	Monster
	+FLOAT
	+NOGRAVITY
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTFALL
	+NOICEDEATH
	+FRIENDLY
	+NOBLOCKMONST
	+THRUACTORS
	+NOBLOOD
	+LOOKALLAROUND
	+NOTAUTOAIMED
	+NOFEAR
	+QUICKTORETALIATE
	+DONTHARMSPECIES
	AttackSound "NoSound"
	PainSound "Weapons/BLG/Spark"
	DeathSound "Weapons/BFGX"
	ActiveSound "NoSound"
	Obituary "%o was zapped by a %k's BLG Seeker!"
	var int user_blg;
	States
	{
	Spawn:
		TNT1 A 0
	Idle:
		TNT1 A 0
        TNT1 A 0 A_JumpIf(user_blg >= 25, "FadeAway")
        TNT1 A 0 A_SetUserVar("user_blg", user_blg+1)
		TNT1 A 0 A_PlaySound("Weapons/BLG/altfly",CHAN_7,1.0,1)
		B3S1 ABCDEFGH 3 Bright A_Wander
		TNT1 A 0 A_Look
		Loop
	See:
		TNT1 A 0
        TNT1 A 0 A_JumpIf(user_blg >= 25, "FadeAway")
        TNT1 A 0 A_SetUserVar("user_blg", user_blg+1)
		TNT1 A 0 A_PlaySound("Weapons/BLG/altfly",CHAN_7,1.0,1)
		B3S1 ABCDEFGH 3 Bright A_Chase
		Loop
	Melee:
	Missile:
		B3S1 AB 2 Bright A_FaceTarget
		B3S1 C 2 Bright A_CustomMissile("BLGSeeker",10)
		B3S1 D 2 Bright
		Goto See
	Pain:
		B3S1 A 2 bright
		B3S1 B 2 bright A_Pain
		goto See
	FadeAway:
		TNT1 A 0 A_PlaySound ("Weapons/PlasmaX",CHAN_7)
		BG3E ABCD 3 Bright
		Stop
	Death:
		BG3X A 3 Bright A_PlaySound ("Weapons/BFGX",CHAN_7)
		BG3X B 3 Bright A_Explode(192,256,FALSE)
		BG3X C 3 Bright
		BG3X D 3 Bright A_NoBlocking
		BG3X EF 3 Bright
		Stop
	}
}


actor BLGSeeker : DoomImpBall
{
	Radius 11
	Height 6
	Speed 30
	Damage ((Random(1,6)+Random(1,6))*16 + 64)
	SCALE 0.60
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	+SEEKERMISSILE
	+EXTREMEDEATH
	+FOILINVUL
	+MTHRUSPECIES
	+DONTHARMSPECIES
	damagetype "Lightning"
	DeathSound "missile/die"
	Obituary "%o couldn't hide from %k's BLG's elecric fury!"
	States
	{
	Spawn:
		B3S2 A 0 a_seekermissile(5,5,SMF_LOOK,256,10)
		B3S2 A 0 a_playsound("BLG/boltcrackle")
		B3S2 A 1 A_CustomMissile("BLGSeekerTrail",0,0,0,0)
		B3S2 A 0 a_seekermissile(5,5,SMF_LOOK,256,10)
		B3S2 A 1 A_CustomMissile("BLGSeekerTrail",0,0,0,0)
		B3S2 A 0 a_seekermissile(5,5,SMF_LOOK,256,10)
		B3S2 A 1 A_CustomMissile("BLGSeekerTrail",0,0,0,0)
		B3S2 B 0 a_seekermissile(5,5,SMF_LOOK,256,10)
		B3S2 B 1 A_CustomMissile("BLGSeekerTrail",0,0,0,0)
		B3S2 B 0 a_seekermissile(5,5,SMF_LOOK,256,10)
		B3S2 B 1 A_CustomMissile("BLGSeekerTrail",0,0,0,0)
		B3S2 B 0 a_seekermissile(5,5,SMF_LOOK,256,10)
		B3S2 B 1 A_CustomMissile("BLGSeekerTrail",0,0,0,0)
		Loop        
	Death:
		TNT1 A 0 A_SetScale(0.5)
		G3SP ABCDEF 2 Bright 
		Stop
	}
}


ACTOR BLGSeekerTrail
{
	Radius 1
	Height 2
	Speed 0
	Damage 0
	Scale 1.0
	Projectile
	+THRUACTORS
	RENDERSTYLE ADD
	ALPHA 0.5
	States
	{
	Spawn:
		PLAZ ABCABCABC 1 Bright A_SetScale(ScaleX-0.1)
	Death:
		TNT1 A 1
		Stop
	}
}
